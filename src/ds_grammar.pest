main       = { SOI ~ tensor* ~ EOI }
tensor     = { name_ij ~ "{" ~ param_elmt? ~ (DELIM~ param_elmt)* ~ DELIM? ~ "}" }
param_elmt = { parameter | tensor_elmt }
indices   = { "(" ~ indice ~ ("," ~ indice)* ~ ")" ~ ":" }
indice      = { integer ~ ( range_sep ~ integer )? }
tensor_elmt = { indices? ~ expression }
parameter  = { name ~ "->" ~  domain ~ ("=" ~ expression)? }
domain     = { (range | name) ~ ("**" ~ integer)? }
range      = { "[" ~ range_val ~ "," ~ range_val ~ "]" }
range_val  = { real | integer | inf | neg_inf }
inf        = { "inf" }
neg_inf    = { "-inf" }
assignment = { name_ij ~ "=" ~ expression }
expression = { sign? ~ term ~ (term_op ~ term)* }
term       = { factor ~ (factor_op ~ factor)* }
factor     = { call | real | integer | name_ij | "(" ~ expression ~ ")" }
call       = { name ~ "(" ~ call_arg ~ ("," ~ call_arg )* ~ ")" }
call_arg   = { expression }
name_ij    = ${ name ~ ("_" ~ name)? }
range_sep  = @{ ".." | ":" }
sign       = @{ ("-"|"+") }
term_op    = @{ "-"|"+" }
factor_op  = @{ "*"|"/" }
name       = @{ ( 'a'..'z' | 'A'..'Z' ) ~ ('a'..'z' | 'A'..'Z' | '0'..'9')* }
integer    = @{ ('0'..'9')+ }
real       = @{ ( ('0'..'9')+ ~ "." ~ ('0'..'9')+ ) | integer }
DELIM      = _{ "," }
WHITESPACE = _{ " " | NEWLINE | "\t" }
COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }


